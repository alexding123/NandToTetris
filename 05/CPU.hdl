// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // type of instruction
    Not(in=instruction[15], out=AInstruction);
    Not(in=AInstruction, out=CInstruction);

    Mux16(a=ALUOut, b=instruction, sel=AInstruction, out=AInput);
    ARegister(in=AInput, load=ALoad, out=ARegOut, out[0..14]=addressM);
    DRegister(in=ALUOut, load=DLoad, out=DRegOut);

    Mux16(a=ARegOut, b=inM, sel=instruction[12], out=AMRegOut);

    ALU(x=DRegOut, y=AMRegOut, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUOut, out=outM, zr=IsZero, ng=IsNeg);

    Or(a=AInstruction, b=instruction[5], out=ALoad);
    And(a=CInstruction, b=instruction[4], out=DLoad);
    And(a=CInstruction, b=instruction[3], out=writeM);

    Not(in=IsNeg, out=NotNeg);
    Not(in=IsZero, out=NotZero);
    And(a=NotNeg, b=NotZero, out=IsPos);

    And(a=IsNeg, b=instruction[2], out=JumpNeg);
    And(a=IsZero, b=instruction[1], out=JumpZero);    
    And(a=IsPos, b=instruction[0], out=JumpPos);

    Or(a=JumpNeg, b=JumpZero, out=JumpNegZero);
    Or(a=JumpNegZero, b=JumpPos, out=JumpAll);

    And(a=CInstruction, b=JumpAll, out=ShouldJump);
    PC(in=ARegOut, load=ShouldJump, inc=true, reset=reset, out[0..14]=pc);
}